
DISTRO = "mng"
DISTRO_NAME = "Mercury Next Generation"
DISTRO_VERSION = "0.1"

SDK_VENDOR = "-mngsdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"
SDK_NAME = "${DISTRO}-${TCLIBC}-${SDK_ARCH}-${TARGET_ARCH}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

MAINTAINER = "Andre McCurdy <armccurdy@gmail.com>"

INCOMPATIBLE_LICENSE = "GPLv3.0 GPLv3 GPL-3.0 GPLv3+"
INCOMPATIBLE_LICENSE += "LGPLv3.0 LGPLv3 LGPL-3.0 LGPLv3+"
INCOMPATIBLE_LICENSE += "AGPLv3.0 AGPLv3 AGPL-3.0 AGPLv3+"

# Allow a few selected GPLv3 packages (for use in local debug builds only).
WHITELIST_GPL-3.0 = "gdb"

IMAGE_LINGUAS = ""
ENABLE_BINARY_LOCALE_GENERATION = "0"

USE_NLS = "no"

MODULE_TARBALL_DEPLOY = "0"

# Prevent kernel being included in rootfs images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""

TCLIBC ?= "musl"

TCLIBCAPPEND = ""

QEMU_TARGETS = "arm armeb i386"

# ----------------------------------------------------------------------------

# Optimise for size by default, but platforms which don't have a problem with
# code size should over-ride by setting OPTIMIZE_DEFAULT to -O2 in their
# machine configs.

OPTIMIZE_DEFAULT ?= "-Os"

OPTIMIZE_DEFAULT_pn-glibc = "-O2"
OPTIMIZE_DEFAULT_pn-musl = "-O2"
OPTIMIZE_DEFAULT_pn-openssl = "-O2"
OPTIMIZE_DEFAULT_pn-tinymembench = "-O2"

FULL_OPTIMIZATION = "${OPTIMIZE_DEFAULT} -pipe ${DEBUG_FLAGS}"

# ----------------------------------------------------------------------------

# By default, "version-going-backwards" is reported as an error.
# However we disable the test completely, since we don't care about package
# feeds and version numbers which differ only in their git revisions often do
# appear to go backwards.
ERROR_QA_remove = "version-going-backwards"
# WARN_QA_append = " version-going-backwards"

# ----------------------------------------------------------------------------

BB_DANGLINGAPPENDS_WARNONLY = "true"

# ----------------------------------------------------------------------------

DISTRO_FEATURES_BACKFILL_CONSIDERED = "pulseaudio bluez5 gobject-introspection-data ldconfig"

DISTRO_FEATURES_remove = "alsa bluetooth ext2 irda pcmcia usbgadget usbhost wifi nfs zeroconf 3g nfc x11"

# ----------------------------------------------------------------------------

# Use busybox/mdev for system initialization

VIRTUAL-RUNTIME_dev_manager = "busybox-mdev"
VIRTUAL-RUNTIME_login_manager = "busybox"
VIRTUAL-RUNTIME_init_manager = "busybox"
VIRTUAL-RUNTIME_initscripts = "initscripts"

DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"

# ----------------------------------------------------------------------------

# gmp 4.2.1 is the last version released as LGPLv2
# nettle 2.7.1 is the last version released as LGPLv2
# gnutls 3.3.x is the last branch which supports nettle 2.7.1
PREFERRED_VERSION_gmp = "4.2.1"
PREFERRED_VERSION_nettle = "2.7.1"
PREFERRED_VERSION_gnutls = "3.3.%"

# Use gdb's bundled version of readline (avoids using the ancient version from meta-gplv2)
PACKAGECONFIG_remove_pn-gdb = "readline"

# ----------------------------------------------------------------------------

require include/mng-${TCLIBC}.inc
require include/mng-security-flags.inc
require include/mng-sstate.inc

# ----------------------------------------------------------------------------
